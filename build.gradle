plugins {
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.1.11'
    id 'java'
}

group = 'nordin.prison.core'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'placeholderapi'
        url = 'https://repo.extendedclip.com/releases/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'enginehub'
        url = 'https://maven.enginehub.org/repo/'
    }
    mavenCentral()
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.10'
    implementation 'net.kyori:adventure-api:4.17.0'
    implementation 'net.kyori:adventure-text-minimessage:4.17.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.4.1'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.13.0'
}

processResources {
    filesMatching("**/plugin.yml") {
        expand(name: rootProject.name, version: project.version, package: rootProject.group)
    }
}

checkstyle {
    toolVersion = '10.23.1'
    maxWarnings = 0
}

configurations.checkstyle {
    resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
        select("com.google.guava:guava:23.0")
    }
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
    }
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports.create("html") {
        required = true
    }
    reports.create("xml") {
        required = false
    }
    ignoreFailures = true
}
